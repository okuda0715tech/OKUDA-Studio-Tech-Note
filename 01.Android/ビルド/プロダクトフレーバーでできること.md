<!-- TOC START min:1 max:3 link:true asterisk:false update:true -->
- [プロダクトフレーバーでできること](#プロダクトフレーバーでできること)
  - [プロパティ一覧](#プロパティ一覧)
  - [プロダクトフレーバーごとにアプリケーション ID を変更する](#プロダクトフレーバーごとにアプリケーション-id-を変更する)
  - [ライブラリに存在しないプロダクトフレーバーがアプリのプロダクトフレーバーに存在している場合](#ライブラリに存在しないプロダクトフレーバーがアプリのプロダクトフレーバーに存在している場合)
  - [アプリに存在しないフレーバーディメンションがライブラリのフレーバーディメンションに存在している場合](#アプリに存在しないフレーバーディメンションがライブラリのフレーバーディメンションに存在している場合)
<!-- TOC END -->


# プロダクトフレーバーでできること

## プロパティ一覧

プロパティ名        | 用途
--------------------|-------------------------------------------------------------------------------------------------------
applicationId       | そのプロダクトフレーバーに適用するアプリケーション ID
applicationIdSuffix | 元のアプリケーション ID の末尾にこの値を付与して、このプロダクトフレーバーのアプリケーション ID とする
versionNameSuffix  |  元のバージョンネームの末尾にこの値を付与して、このプロダクトフレーバーのバージョンネームとする
matchingFallbacks  |  プロダクトフレーバーが、アプリのプロダクトフレーバーには存在するが、ライブラリのプロダクトフレーバーには存在しない場合の対処

## プロダクトフレーバーごとにアプリケーション ID を変更する

アプリケーション ID は、デバイス内や Play ストア内で、アプリケーションを一意に識別する ID である。

アプリの「無料版」と「有料版」を別々のアプリとして公開したい場合などに ID を別々に設定します。

```java
android {
  defaultConfig {
    applicationId "com.example.myapp"
  }
  productFlavors {
    free {
      // 方法 1
      // defaultConfig のアプリケーション ID の末尾にセグメントを追加して
      // 新しいアプリケーション ID とする方法
      applicationIdSuffix ".free"
    }
    pro {
      // 方法 2
      // 別のアプリケーション ID で上書きする方法
      applicationId "com.example.myapp.pro"
    }
  }
}
```


## ライブラリに存在しないプロダクトフレーバーがアプリのプロダクトフレーバーに存在している場合

以下の例は、アプリの `build.gradle` の、 charges フレーバーディメンションには、  
free プロダクトフレーバーが存在するが、  
ライブラリの `build.gradle` の、 charges フレーバーディメンションには、  
free プロダクトフレーバーが存在しない場合の対処例です。

```java
// これはアプリ側の build.gradle ファイルを示しています。
android {
  defaultConfig{
    // defaultConfig ブロック内では、 matchingFallbacks を定義してはいけません。
  }
  flavorDimensions 'charges'
  productFlavors {
    paid {
      dimension 'charges'
      // paid プロダクトフレーバーはライブラリ内にも存在するため、 matchingFallbacks を
      // 定義する必要はありません。
    }
    free {
      dimension 'charges'
      // 先頭のパラメータから対応するプロダクトフレーバーが存在しているかをチェックしていき、
      // 存在している場合に、そのプロダクトフレーバーを適用します。
      // パラメータはいくつでも指定することができます。
      // 対応するプロダクトフレーバーが存在しない場合にはビルドエラーになります。
      matchingFallbacks = ['demo', 'trial']
    }
  }
}
```


## アプリに存在しないフレーバーディメンションがライブラリのフレーバーディメンションに存在している場合

次のサンプルに示すように、 `defaultConfig` ブロックで `missingDimensionStrategy` を使用して、  
見つからなかった各ディメンションの中からプラグインで選択すべきデフォルトのフレーバーディメンションを指定します。

また、productFlavors ブロック内で選択肢をオーバーライドして、存在しないフレーバーディメンションに対して  
プロダクトフレーバーごとに異なるマッチング方針を指定することもできます。

```java
// これはアプリ側の build.gradle ファイルを示しています。
android {
  defaultConfig{
    missingDimensionStrategy 'minApi', 'minApi18', 'minApi23'
    // 分類ごとに missingDimensionStrategy の記述を行います
    missingDimensionStrategy 'abi', 'x86', 'arm64'
  }
  flavorDimensions 'tier'
  productFlavors {
    free {
      dimension 'tier'
      // defaultConfig で指定した missingDimensionStrategy を各プロダクトフレーバー内で
      // オーバーライドすることができます。
      missingDimensionStrategy 'minApi', 'minApi23', 'minApi18'
    }
    paid {}
  }
}
```
